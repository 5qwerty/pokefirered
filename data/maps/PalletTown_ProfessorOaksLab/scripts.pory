const SOAK_ID = 1
const SQUIRTLE_ID = 2
const TOTODILE_ID = 3
const MUDKIP_ID = 4
const RIVAL_ID = 5
const AIDE_1_ID	= 6

const PLAYER_STARTER_NUM = VAR_TEMP_1
const PLAYER_STARTER_SPECIES = VAR_TEMP_2

mapscripts PalletTown_ProfessorOaksLab_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 1, PalletTown_SoakLab_EventScript_ReadySoakRivalForStarterScene)
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 1: PalletTown_SoakLab_EventScript_ReadyPlayerForStarterScene
	]
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 1: PalletTown_SoakLab_EventScript_ChooseStarterScene
	]
}

script PalletTown_SoakLab_EventScript_ReadySoakRivalForStarterScene {
	setobjectmovementtype(RIVAL_ID, MOVEMENT_TYPE_FACE_UP)
	setobjectxyperm(AIDE_1_ID, 6, 11)
	setobjectmovementtype(AIDE_1_ID, MOVEMENT_TYPE_FACE_UP)
	savebgm(MUS_OAK)
	return
}

script PalletTown_SoakLab_EventScript_ReadyPlayerForStarterScene {
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	end
}

script PalletTown_SoakLab_EventScript_ChooseStarterScene {
	lockall
	textcolor(NPC_TEXT_COLOR_MALE)
	applymovement(AIDE_1_ID, PalletTown_SoakLab_Movement_Up3)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_Up3)
	waitmovement(0)
	applymovement(AIDE_1_ID, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(PalletTown_SoakLab_Text_GoAhead)
	closemessage
	applymovement(AIDE_1_ID, PalletTown_SoakLab_Movement_AideRight)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_PlayerToSpot)
	waitmovement(0)
	setobjectmovementtype(AIDE_1_ID, MOVEMENT_TYPE_LOOK_AROUND)
	applymovement(RIVAL_ID, Common_Movement_WalkInPlaceFasterUp)
	clearflag(FLAG_DONT_TRANSITION_MUSIC)
	savebgm(MUS_DUMMY)
	fadedefaultbgm
	msgbox(PalletTown_SoakLab_Text_TiredOfWaiting)
	closemessage
	delay(30)
	msgbox(PalletTown_SoakLab_Text_ChooseThree)
	closemessage
	delay(30)
	applymovement(RIVAL_ID, Common_Movement_WalkInPlaceUp)
	waitmovement(0)
	msgbox(PalletTown_SoakLab_Text_CompletelyUnfair)
	closemessage
	//applymovement(RIVAL_ID, Common_Movement_FaceRight)
	//applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	//waitmovement(0)
	//msgbox(PalletTown_SoakLab_Text_OuttaTheWay)
	//closemessage
	//delay(30)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_PlayerOuttaWay)
	applymovement(SOAK_ID, PalletTown_SoakLab_Movement_DelayLookRight)
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_ToMudkip)
	waitmovement(0)
	msgbox(PalletTown_SoakLab_Text_IWantedThis)
	closemessage
	delay(30)
	removeobject(MUDKIP_ID)
	textcolor(NPC_TEXT_COLOR_NEUTRAL)
	bufferspeciesname(STR_VAR_1, SPECIES_MUDKIP)
	message(PalletTown_SoakLab_Text_RivalReceivedFromSoak)
	waitmessage
	playfanfare(MUS_OBTAIN_KEY_ITEM)
	waitfanfare
	delay(30)
	textcolor(NPC_TEXT_COLOR_MALE)
	applymovement(SOAK_ID, Common_Movement_WalkInPlaceFasterDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	msgbox(PalletTown_SoakLab_Text_ChooseTwo)
	closemessage
	setvar(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 2)
	releaseall
	end
}

text PalletTown_SoakLab_Text_GoAhead {
	format("Please go ahead.")
}

text PalletTown_SoakLab_Text_TiredOfWaiting {
	format("{RIVAL}: Gramps!\nWhy'd you make me wait so long just\lfor this loser?")
}

text PalletTown_SoakLab_Text_ChooseThree {
	format("SOAK: {RIVAL}!\nDon't be mean, little dude!\pI had already decided that {PLAYER} would get to choose first today.\p"
		   "Now, {PLAYER}, I have three POKéMON in the POKé BALLS here.\pYou can go ahead and have one!\nC'mon, one of these is all yours!")
}

text PalletTown_SoakLab_Text_CompletelyUnfair {
	format("{RIVAL}: What?\nThat's dumb and completely unfair!\pI'm the one who's your grandson!\nI should get to choose first no\lmatter what!")
}

text PalletTown_SoakLab_Text_OuttaTheWay {
	format("{RIVAL}: Out of my way, you idiot!\pI'm picking first no matter what!")
}

text PalletTown_SoakLab_Text_IWantedThis {
	format("{RIVAL}: Yes, I've had my eye on\nthis one for a long time!\pI choose you!")
}

text PalletTown_SoakLab_Text_ChooseTwo {
	format("SOAK: Yeesh...\nSomebody oughta teach him some manners some day...\pWell at least there are two POKéMON left, {PLAYER}.\pGo ahead an choose one!")
}

text PalletTown_SoakLab_Text_RivalReceivedFromSoak {
	format("{RIVAL} received the {STR_VAR_1} from PROF. SOAK!")
}

movement PalletTown_SoakLab_Movement_Up3 {
	walk_up * 3
	step_end
}

movement PalletTown_SoakLab_Movement_AideRight {
	walk_right
	walk_down * 2
	walk_right * 2
	step_end
}

movement PalletTown_SoakLab_Movement_PlayerToSpot {
	walk_up * 5
	step_end
}

movement PalletTown_SoakLab_Movement_PlayerOuttaWay {
	walk_fast_down
	walk_in_place_fast_up
	walk_in_place_fast_right
	step_end
}

movement PalletTown_SoakLab_Movement_DelayLookRight {
	delay_16
	walk_in_place_fast_right
	step_end
}

movement PalletTown_SoakLab_Movement_ToMudkip {
	walk_right * 2
	walk_down
	walk_right * 3
	face_up
	step_end
}

script PalletTown_SoakLab_EventScript_SquirtleBall {
	lock
	faceplayer
	setvar(PLAYER_STARTER_NUM, 0)
	setvar(PLAYER_STARTER_SPECIES, SPECIES_SQUIRTLE)
	goto_if_eq(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 2, PalletTown_SoakLab_EventScript_ConfirmStarterChoice)
	msgbox(PalletTown_SoakLab_Text_SoakLastMon)
	release
	end	
}

script PalletTown_SoakLab_EventScript_TotodileBall {
	lock
	faceplayer
	setvar(PLAYER_STARTER_NUM, 1)
	setvar(PLAYER_STARTER_SPECIES, SPECIES_TOTODILE)
	goto_if_eq(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 2, PalletTown_SoakLab_EventScript_ConfirmStarterChoice)
	msgbox(PalletTown_SoakLab_Text_SoakLastMon)
	release
	end	
}

script PalletTown_SoakLab_EventScript_ConfirmStarterChoice {
	applymovement(SOAK_ID, Common_Movement_FaceRight)
	waitmovement(0)
	showmonpic(PLAYER_STARTER_SPECIES, 10, 3)
	textcolor(NPC_TEXT_COLOR_MALE)
	goto_if_eq(PLAYER_STARTER_NUM, 0, PalletTown_SoakLab_EventScript_ConfirmSquirtle)
	goto_if_eq(PLAYER_STARTER_NUM, 1, PalletTown_SoakLab_EventScript_ConfirmTotodile)
	end
}

script PalletTown_SoakLab_EventScript_ConfirmSquirtle {
	msgbox(PalletTown_SoakLab_Text_ChoosingSquirtle, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, YES, PalletTown_SoakLab_EventScript_ChoseStarter)
	goto_if_eq(VAR_RESULT, NO, PalletTown_SoakLab_EventScript_DeclinedStarter)
	end
}

script PalletTown_SoakLab_EventScript_ConfirmTotodile {
	msgbox(PalletTown_SoakLab_Text_ChoosingTotodile, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, YES, PalletTown_SoakLab_EventScript_ChoseStarter)
	goto_if_eq(VAR_RESULT, NO, PalletTown_SoakLab_EventScript_DeclinedStarter)
	end
}

script PalletTown_SoakLab_EventScript_DeclinedStarter {
	hidemonpic
	release
	end
}

script PalletTown_SoakLab_EventScript_ChoseStarter {
	hidemonpic
	removeobject(VAR_LAST_TALKED)
	msgbox(PalletTown_SoakLab_Text_Energetic)
	call(EventScript_RestorePrevTextColor)
	setflag(FLAG_SYS_POKEMON_GET)
	givemon(PLAYER_STARTER_SPECIES, 98)
	copyvar(VAR_STARTER_MON, PLAYER_STARTER_NUM)
	bufferspeciesname(STR_VAR_1, PLAYER_STARTER_SPECIES)
	message(PalletTown_SoakLab_Text_ReceivedMonFromSoak)
	waitmessage
	playfanfare(MUS_OBTAIN_KEY_ITEM)
	waitfanfare
	msgbox(Text_GiveNicknameToThisMon, MSGBOX_YESNO)
	closemessage
	setvar(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 3)
	goto_if_eq(VAR_RESULT, YES, EventScript_GiveNicknameToStarter)
	end
}

script EventScript_GiveNicknameToStarter {
	setvar(VAR_0x8004, 0)
	call(EventScript_ChangePokemonNickname)
	end
}

text PalletTown_SoakLab_Text_SoakLastMon {
	format("That's PROF. SOAK's last POKéMON!")
}

text PalletTown_SoakLab_Text_ChoosingSquirtle {
	format("SOAK: Cool pick!\nIt's definitely one worth raising.\pSo, {PLAYER}, you're grabbing the\nWATER POKéMON SQUIRTLE?")
}

text PalletTown_SoakLab_Text_ChoosingTotodile {
	format("SOAK: Solid choice!\nThis one's pretty easy to raise.\pSo, {PLAYER}, you're going with the\nWATER POKéMON TOTODILE?")
}

text PalletTown_SoakLab_Text_Energetic {
	format("This POKéMON seems pretty gnarly,\nno cap!")
}

text PalletTown_SoakLab_Text_ReceivedMonFromSoak {
	format("{PLAYER} received the {STR_VAR_1} from PROF. SOAK!")
}

script PalletTown_SoakLab_EventScript_RivalBattleTriggerLeft {
	lockall
	setvar(VAR_TEMP_2, 3)
	goto(PalletTown_SoakLab_EventScript_RivalBattle)
	end
}

script PalletTown_SoakLab_EventScript_RivalBattleTriggerMid {
	lockall
	setvar(VAR_TEMP_2, 2)
	goto(PalletTown_SoakLab_EventScript_RivalBattle)
	end
}

script PalletTown_SoakLab_EventScript_RivalBattleTriggerRight {
	lockall
	setvar(VAR_TEMP_2, 1)
	goto(PalletTown_SoakLab_EventScript_RivalBattle)
	end
}

script PalletTown_SoakLab_EventScript_RivalBattle {
	textcolor(NPC_TEXT_COLOR_MALE)
	playbgm(MUS_ENCOUNTER_RIVAL, 0)
	applymovement(RIVAL_ID, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
	msgbox(PalletTown_SoakLab_Text_BeatDown)
	closemessage
	applymovement(SOAK_ID, Common_Movement_FaceDown)
	waitmovement(0)
	call_if_eq(VAR_TEMP_2, 3, PalletTown_SoakLab_EventScript_RivalToPlayerLeft)
	call_if_eq(VAR_TEMP_2, 2, PalletTown_SoakLab_EventScript_RivalToPlayerMid)
	call_if_eq(VAR_TEMP_2, 1, PalletTown_SoakLab_EventScript_RivalToPlayerRight)
	trainerbattle_earlyrival(TRAINER_RIVAL_SOAK_LAB, RIVAL_BATTLE_TUTORIAL, PalletTown_SoakLab_Text_RivalDefeat, PalletTown_SoakLab_Text_RivalVictory)
	special(HealPlayerParty)
	msgbox(PalletTown_SoakLab_Text_RivalLeave)
	closemessage
	playbgm(MUS_RIVAL_EXIT, 0)
	call_if_eq(VAR_TEMP_2, 3, PalletTown_SoakLab_EventScript_RivalLeaveLeft)
	call_if_eq(VAR_TEMP_2, 2, PalletTown_SoakLab_EventScript_RivalLeaveMid)
	call_if_eq(VAR_TEMP_2, 1, PalletTown_SoakLab_EventScript_RivalLeaveRight)
	removeobject(RIVAL_ID)
	playse(SE_EXIT)
	fadedefaultbgm
	setvar(VAR_MAP_SCENE_PALLET_TOWN_PROFESSOR_OAKS_LAB, 4)
	setflag(FLAG_BEAT_RIVAL_IN_OAKS_LAB) //Change later
	releaseall
	end
}

script PalletTown_SoakLab_EventScript_RivalToPlayerLeft {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalToPlayerLeft)
	waitmovement(0)
	return
}

script PalletTown_SoakLab_EventScript_RivalToPlayerMid {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalToPlayerMid)
	waitmovement(0)
	return
}

script PalletTown_SoakLab_EventScript_RivalToPlayerRight {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalToPlayerRight)
	waitmovement(0)
	return
}

script PalletTown_SoakLab_EventScript_RivalLeaveLeft {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalExitLeft)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_PlayerWatchLeftMid)
	waitmovement(RIVAL_ID)
	return
}

script PalletTown_SoakLab_EventScript_RivalLeaveMid {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalExitMid)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_PlayerWatchLeftMid)
	waitmovement(RIVAL_ID)
	return
}

script PalletTown_SoakLab_EventScript_RivalLeaveRight {
	applymovement(RIVAL_ID, PalletTown_SoakLab_Movement_RivalExitRight)
	applymovement(OBJ_EVENT_ID_PLAYER, PalletTown_SoakLab_Movement_PlayerWatchRight)
	waitmovement(RIVAL_ID)
	return
}

text PalletTown_SoakLab_Text_BeatDown {
	format("{RIVAL}: Huh?\nTrying to leave?\pLet me give you a beatdown first!")
}

text PalletTown_SoakLab_Text_RivalDefeat {
	format("What?\nThis isn't possible!\pYou only won because of the tips\nmy gramps gave!")
}

text PalletTown_SoakLab_Text_RivalVictory {
	format("Oh yeah, my MUDKIP stomped you!\nAren't I the greatest?")
}

text PalletTown_SoakLab_Text_RivalLeave {
	format("{RIVAL}: Sniff ya later, loser!\nI'm outta here!")
}

movement PalletTown_SoakLab_Movement_RivalToPlayerLeft {
	walk_left * 5
	walk_down * 2
	step_end
}

movement PalletTown_SoakLab_Movement_RivalToPlayerMid {
	walk_left * 4
	walk_down * 2
	step_end
}

movement PalletTown_SoakLab_Movement_RivalToPlayerRight {
	walk_left * 3
	walk_down * 2
	step_end
}

movement PalletTown_SoakLab_Movement_RivalExitLeft {
	walk_right
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	delay_8
	step_end
}

movement PalletTown_SoakLab_Movement_RivalExitMid {
	walk_right
	walk_down
	walk_down
	walk_down
	walk_left
	walk_down
	walk_down
	delay_8
	step_end
}

movement PalletTown_SoakLab_Movement_RivalExitRight {
	walk_left
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	delay_8
	step_end
}

movement PalletTown_SoakLab_Movement_PlayerWatchLeftMid {
	delay_16
	delay_8
	walk_in_place_faster_right
	delay_16
	walk_in_place_faster_down
	step_end
}

movement PalletTown_SoakLab_Movement_PlayerWatchRight {
	delay_16
	delay_8
	walk_in_place_faster_left
	delay_16
	walk_in_place_faster_down
	step_end
}


script PalletTown_SoakLab_Event_LeaveStarterSceneTrigger {
	lockall
	textcolor(NPC_TEXT_COLOR_MALE)
	applymovement(SOAK_ID, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(PalletTown_SoakLab_Text_DontLeave)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	releaseall
	end
}

text PalletTown_SoakLab_Text_DontLeave {
	format("SOAK: Ayo!\nDon't leave yet!")
}