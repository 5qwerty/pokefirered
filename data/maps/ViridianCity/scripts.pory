const DAISY_ID = 1
const OLD_MAN_ID = 2
const OLD_WOMAN_ID = 3
const CHILD_ID = 4

mapscripts ViridianCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setworldmapflag(FLAG_WORLD_MAP_VIRIDIAN_CITY)
		call_if_lt(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, ViridianCity_EventScript_SetOldManBlockingRoad)
		call_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, ViridianCity_EventScript_SetOldManNormal)
	}
}

script ViridianCity_EventScript_SetOldManBlockingRoad {
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_OLD_MAN_LYING_DOWN)
	setobjectxyperm(OLD_MAN_ID, 14, 10)
	setobjectmovementtype(OLD_MAN_ID, MOVEMENT_TYPE_FACE_DOWN)
}

script ViridianCity_EventScript_SetOldManNormal {
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_OLD_MAN_1)
	setobjectxyperm(OLD_MAN_ID, 20, 9)
	setobjectxyperm(OLD_WOMAN_ID, 21, 9)
	setobjectxyperm(CHILD_ID, 17, 13)
	setobjectmovementtype(OLD_MAN_ID, MOVEMENT_TYPE_FACE_DOWN)
	setobjectmovementtype(OLD_WOMAN_ID, MOVEMENT_TYPE_FACE_DOWN)
	setobjectmovementtype(CHILD_ID, MOVEMENT_TYPE_WANDER_AROUND)
}

script ViridianCity_EventScript_MartTrigger {
	clearflag(FLAG_HIDE_DAISY_VIRIDIAN)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	opendoor(38, 6)
	waitdooranim
	addobject(DAISY_ID)
	applymovement(DAISY_ID, ViridianCity_Movement_WalkDown)
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_PlayerDown)
	waitmovement(0)
	closedoor(38, 6)
	waitdooranim
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	applymovement(DAISY_ID, Common_Movement_ExclamDown)
	waitmovement(0)
	msgbox(ViridianCity_Text_HiPlayer)
	closemessage
	applymovement(DAISY_ID, ViridianCity_Movement_DaisyToHouse)
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_PlayerToHouse)
	waitmovement(0)
	opendoor(41, 6)
	waitdooranim
	applymovement(DAISY_ID, ViridianCity_Movement_DaisyEnterHouse)
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_PlayerEnterHouse)
	waitmovement(0)
	closedoor(41, 6)
	waitdooranim
	setflag(FLAG_HIDE_DAISY_VIRIDIAN)
	warp(MAP_VIRIDIAN_CITY_MAISY_HOUSE, 4, 7)
	waitstate
	releaseall
}

movement ViridianCity_Movement_WalkDown {
	walk_down
	step_end
}

movement ViridianCity_Movement_PlayerDown {
	face_up
	walk_down
	step_end
}

text ViridianCity_Text_HiPlayer {
	format("MAISY: {PLAYER}!\nWhat a neat coincidence!\pI was just picking up grandpa's\nPARCEL from the POKÃ© MART here.\pHe got some stuff for you too, so\nwhy don't you come with me?")
}

movement ViridianCity_Movement_DaisyToHouse {
	walk_right * 3
	step_end
}

movement ViridianCity_Movement_PlayerToHouse {
	walk_up
	walk_right * 2
	step_end
}

movement ViridianCity_Movement_DaisyEnterHouse {
	walk_up
	set_invisible
	step_end
}

movement ViridianCity_Movement_PlayerEnterHouse {
	walk_right
	walk_up
	set_invisible
	step_end
}

script ViridianCity_EventScript_MaisyTrigger {
	clearflag(FLAG_HIDE_DAISY_VIRIDIAN)
	textcolor(NPC_TEXT_COLOR_FEMALE)
	opendoor(38, 6)
	waitdooranim
	addobject(DAISY_ID)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	applymovement(DAISY_ID, ViridianCity_Movement_WalkDown)
	waitmovement(0)
	closedoor(38, 6)
	waitdooranim
	playse(SE_PIN)
	applymovement(DAISY_ID, Common_Movement_ExclamRight)
	waitmovement(0)
	msgbox(ViridianCity_Text_HiPlayer)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_PlayerToHouse2)
	applymovement(DAISY_ID, ViridianCity_Movement_DaisyToHouse)
	waitmovement(0)
	opendoor(41, 6)
	waitdooranim
	applymovement(DAISY_ID, ViridianCity_Movement_DaisyEnterHouse)
	applymovement(OBJ_EVENT_ID_PLAYER, ViridianCity_Movement_PlayerEnterHouse2)
	waitmovement(0)
	closedoor(41, 6)
	waitdooranim
	setflag(FLAG_HIDE_DAISY_VIRIDIAN)
	warp(MAP_VIRIDIAN_CITY_MAISY_HOUSE, 4, 7)
	waitstate
	releaseall
}

movement ViridianCity_Movement_PlayerToHouse2 {
	walk_right
	face_left
	step_end
}

movement ViridianCity_Movement_PlayerEnterHouse2 {
	walk_left
	walk_up
	set_invisible
	step_end
}

script ViridianCity_EventScript_CitySign {
	msgbox(ViridianCity_Text_CitySign, MSGBOX_SIGN)	
}

text ViridianCity_Text_CitySign {
	format("FILL")
}

script ViridianCity_EventScript_MaisyHouseSign {
	msgbox(ViridianCity_Text_MaisyHouseSign, MSGBOX_SIGN)	
}

text ViridianCity_Text_MaisyHouseSign {
	format("MAISY'S HOUSE")
}

script ViridianCity_EventScript_OldMan {
	goto_if_le(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, ViridianCity_EventScript_OldManTalk1)
    goto_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, ViridianCity_EventScript_OldManTalk2)
}

script ViridianCity_EventScript_OldWoman {
	goto_if_le(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, ViridianCity_EventScript_OldWomanTalk1)
    goto_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, ViridianCity_EventScript_OldWomanTalk2)
}

script ViridianCity_EventScript_Child {
	goto_if_le(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, ViridianCity_EventScript_ChildTalk1)
    goto_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, ViridianCity_EventScript_ChildTalk2)
}

script ViridianCity_EventScript_OldManTalk1 {
	textcolor(NPC_TEXT_COLOR_NEUTRAL)
	msgbox(ViridianCity_Text_OldManTalk1, MSGBOX_NPC)
}

script ViridianCity_EventScript_OldManTalk2 {
	msgbox(ViridianCity_Text_OldManTalk2, MSGBOX_NPC)
}

script ViridianCity_EventScript_OldWomanTalk1 {
	lock
	msgbox(ViridianCity_Text_OldWomanTalk1, MSGBOX_DEFAULT)
	closemessage
	release
	end
}

script ViridianCity_EventScript_OldWomanTalk2 {
	msgbox(ViridianCity_Text_OldWomanTalk2, MSGBOX_NPC)
}

script ViridianCity_EventScript_ChildTalk1 {
	lock
	msgbox(ViridianCity_Text_ChildTalk1, MSGBOX_DEFAULT)
	closemessage
	release
	end
}

script ViridianCity_EventScript_ChildTalk2 {
	msgbox(ViridianCity_Text_ChildTalk2, MSGBOX_NPC)
}

text ViridianCity_Text_OldManTalk1 {
	"It looks like he's out cold."
}

text ViridianCity_Text_OldManTalk2 {
	"FILL"
}

text ViridianCity_Text_OldWomanTalk1 {
	"Dad, you can't just pass out drunk\non the street!"
}

text ViridianCity_Text_OldWomanTalk2 {
	"FILL"
}

text ViridianCity_Text_ChildTalk1 {
	"Grandpa! Are you dead?"
}

text ViridianCity_Text_ChildTalk2 {
	"FILL"
}