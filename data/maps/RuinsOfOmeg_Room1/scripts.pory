const SCI_GUY1 = 1
const GIO = 2
const GIRL = 3
const SCI_GUY2 = 4
const HIKER = 5
const RIVAL = 6


mapscripts RuinsOfOmeg_Room1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq(VAR_MAP_SCENE_OBSIDIAN_CITY_EVENTS, 2, RuinsOfOmeg_Room1_MoveNPCs)
        call_if_ge(VAR_MAP_SCENE_OBSIDIAN_CITY_EVENTS, 3, RuinsOfOmeg_Room1_MoveSciGuy)
	}
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAP_SCENE_OBSIDIAN_CITY_EVENTS, 2: RuinsOfOmeg_Room1_EnterScript
	]
}

script RuinsOfOmeg_Room1_MoveSciGuy {
    setobjectxyperm(SCI_GUY1, 22, 38)
    setobjectmovementtype(SCI_GUY1, MOVEMENT_TYPE_FACE_DOWN)
}

script RuinsOfOmeg_Room1_MoveNPCs {
    setobjectxyperm(GIRL, 5, 39)
    setobjectxyperm(HIKER, 5, 40)
    setobjectxyperm(SCI_GUY2, 6, 40)
    setobjectxyperm(RIVAL, 6, 39)
    setobjectxyperm(SCI_GUY1, 13, 39)
    setobjectmovementtype(GIRL, MOVEMENT_TYPE_FACE_RIGHT)
    setobjectmovementtype(HIKER, MOVEMENT_TYPE_FACE_RIGHT)
    setobjectmovementtype(SCI_GUY2, MOVEMENT_TYPE_FACE_RIGHT)
    setobjectmovementtype(RIVAL, MOVEMENT_TYPE_FACE_RIGHT)
    setobjectmovementtype(SCI_GUY1, MOVEMENT_TYPE_FACE_LEFT)
}

script RuinsOfOmeg_Room1_EnterScript {
    lockall
    applymovement(GIO, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    textcolor(NPC_TEXT_COLOR_MALE)
    msgbox(RuinsOfOmeg_Room1_EveryoneSafe)
    closemessage
    applymovement(SCI_GUY1, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(RuinsOfOmeg_Room1_Explosion)
    closemessage
    playse(SE_PIN)
    applymovement(GIO, Common_Movement_ExclamRight)
    waitmovement(0)
    msgbox(RuinsOfOmeg_Room1_WhyBomb)
    closemessage
    applymovement(SCI_GUY1, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(RuinsOfOmeg_Room1_Corpse)
    closemessage
    applymovement(GIO, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(RuinsOfOmeg_Room1_ContactCinnabar)
    closemessage
    applymovement(GIO, Common_Movement_WalkRight)
    waitmovement(0)
    applymovement(SCI_GUY1, Common_Movement_FaceDown)
    applymovement(GIO, Common_Movement_WalkDown)
    waitmovement(0)
    applymovement(GIO, RuinsOfOmeg_Room1_Movement_Gio)
    applymovement(OBJ_EVENT_ID_PLAYER, RuinsOfOmeg_Room1_Movement_Player)
    waitmovement(0)
    msgbox(RuinsOfOmeg_Room1_Tragedy)
    closemessage
    applymovement(GIO, Common_Movement_FaceDown)
    waitmovement(0)
    playse(SE_EXIT)
    waitse()
    removeobject(GIO)
    playbgm(MUS_RIVAL_EXIT, 0)
    applymovement(RIVAL, RuinsOfOmeg_Room1_Movement_Rival1)
    waitmovement(0) 
    msgbox(RuinsOfOmeg_Room1_RivalTalk)
    closemessage
    applymovement(RIVAL, Common_Movement_FaceDown)
    waitmovement(0) 
    playse(SE_EXIT)
    waitse()
    removeobject(RIVAL)
    addvar(VAR_MAP_SCENE_OBSIDIAN_CITY_EVENTS, 1) //should be 3 after this script
    clearflag(FLAG_HIDE_GIO_GYM)
    fadedefaultbgm
    releaseall
    end
}

text RuinsOfOmeg_Room1_EveryoneSafe {
    format("GEOFFREY: Looks like ev'rybody's\nsafe and accounted for.\pNow tell me, what on earth took\nplace in here?")
}

text RuinsOfOmeg_Room1_Explosion {
    format("Sir, we've confirmed that two\nexplosions took place.\pThe first allowed the criminals to\nenter the chamber containing the\lUNOWN.\pThe second was lethal and killed\nall of the UNOWN in the chamber.")
}

text RuinsOfOmeg_Room1_WhyBomb {
    format("GEOFFREY: Kill'n the UNOWN?!\nWhat purpose does that serve?\pUnless...\nThey're after that old myth?\pTell me, is there anything left in\nthat chamber?")
}

text RuinsOfOmeg_Room1_Corpse {
    format("It appears that the remains of the\nUNOWN have been taken by them.")
}

text RuinsOfOmeg_Room1_ContactCinnabar {
    format("GEOFFREY: Contact CINNABAR ISLAND\nright this moment!\pBesides that, there's ain't much\nwe can do.\pI'm headin' back to the GYM for now.")
}

text RuinsOfOmeg_Room1_Tragedy {
    format("GEOFFREY: What a needless tragedy.\nWho were those people?\pAn'way, I see that fire in yer\neyes, so you better come down to\lthe GYM, ya hear?")
}

text RuinsOfOmeg_Room1_RivalTalk {
    format("{RIVAL}: Taking the corpses of the\nPOKÃ©MON? There is a point in that?\pI wonder what use they have...\nHehehe...\pSniff ya later, dork!")
}

movement RuinsOfOmeg_Room1_Movement_Gio {
    walk_down
    face_right
}

movement RuinsOfOmeg_Room1_Movement_Player {
    walk_right
    face_left
}

movement RuinsOfOmeg_Room1_Movement_Rival1 {
    walk_right * 6
    walk_down * 2
    face_right
}