const RIVAL = 1

mapscripts Route2_EastBuilding_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_MAP_SCENE_ROUTE2, 0: Route2_EastBuilding_RivalBattle
	]
}

script Route2_EastBuilding_RivalBattle {
	lockall
	textcolor(NPC_TEXT_COLOR_MALE)
	playbgm(MUS_ENCOUNTER_RIVAL, FALSE)
	delay(30)
	playse(SE_PIN)
	applymovement(RIVAL, Common_Movement_ExclamUp)
	waitmovement(0)
	msgbox(Route2_EastBuilding_Text_RivalHi)
	closemessage
	applymovement(RIVAL, Route2_EastBuilding_Movement_RivalWalkUp)
	waitmovement(0)
	msgbox(Route2_EastBuilding_Text_RivalHi2)
	closemessage
	trainerbattle_no_intro(TRAINER_RIVAL_ROUTE2, Route2_EastBuilding_Text_RivalLose)
	msgbox(Route2_EastBuilding_Text_RivalBye)
	closemessage
	playbgm(MUS_RIVAL_EXIT, FALSE)
	applymovement(RIVAL, Route2_EastBuilding_Movement_RivalLeave)
	waitmovement(0)
	delay(30)
    removeobject(RIVAL) 
    playse(SE_EXIT)
    waitse()
	fadedefaultbgm
	addvar(VAR_MAP_SCENE_ROUTE2, 1) //Should be 1 after this
	releaseall
	end
}

text Route2_EastBuilding_Text_RivalHi {
	format("{RIVAL}: Oh!\nIf it isn't the nerd!")
}

text Route2_EastBuilding_Text_RivalHi2 {
	format("{RIVAL}: You actually managed to\nget a BADGE?\pHahahaha!\nYou probably lucked out!\pLet's see if you against me can\nwithout gramps' help!")
}

text Route2_EastBuilding_Text_RivalLose {
	format("{RIVAL}: WHAT?\nHow can one be so lucky?!")	
}

text Route2_EastBuilding_Text_RivalBye {
	format("{RIVAL}: Tchh...\nWhatever...\pDon't think you're better than me\njust 'cause of one lousy battle.\pSniff ya later, idiot!")
}

movement Route2_EastBuilding_Movement_RivalWalkUp {
	walk_up * 2
}

movement Route2_EastBuilding_Movement_RivalLeave {
	walk_down * 7
}