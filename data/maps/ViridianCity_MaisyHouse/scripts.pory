const DAISY_ID = 1
const MAP_ID = 2
const DEX_ID = 3

mapscripts ViridianCity_MaisyHouse_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 0, MaisyHouse_EventScript_ReadyDaisy)
        call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1, MaisyHouse_EventScript_ReadyDaisy2)
        call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, MaisyHouse_EventScript_ReadyDaisy2)
	}
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 0: MaisyHouse_EventScript_GiveItems
	]
}

script MaisyHouse_EventScript_ReadyDaisy {
    setflag(FLAG_HIDE_POKEDEX)
    setflag(FLAG_HIDE_TOWN_MAP)
    setobjectxyperm(DAISY_ID, 4, 6)
	setobjectmovementtype(DAISY_ID, MOVEMENT_TYPE_FACE_UP)
}

script MaisyHouse_EventScript_ReadyDaisy2 {
    setobjectxyperm(DAISY_ID, 4, 4)
	setobjectmovementtype(DAISY_ID, MOVEMENT_TYPE_FACE_DOWN)
}

script MaisyHouse_EventScript_GiveItems {
    lockall
	textcolor(NPC_TEXT_COLOR_FEMALE)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    applymovement(DAISY_ID, MaisyHouse_Movement_DaisyMove1)
    waitmovement(0)
    msgbox(MaisyHouse_Text_TheseTwo)
    closemessage
    applymovement(DAISY_ID, MaisyHouse_Movement_RightFaceDown)
    waitmovement(0)
    addobject(MAP_ID)
    clearflag(FLAG_HIDE_TOWN_MAP)
    applymovement(DAISY_ID, MaisyHouse_Movement_RightFaceDown)
    waitmovement(0)
    addobject(DEX_ID)
    clearflag(FLAG_HIDE_POKEDEX)
    applymovement(DAISY_ID, MaisyHouse_Movement_BackToStart)
    waitmovement(0)
    msgbox(MaisyHouse_Text_Take)
    closemessage
    addvar(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1)
    releaseall
    end
}

movement MaisyHouse_Movement_DaisyMove1 {
    walk_up * 3
    face_down
    step_end
}

movement MaisyHouse_Movement_RightFaceDown {
    walk_right
    face_down
    delay_16
    step_end
}

movement MaisyHouse_Movement_BackToStart {
    walk_left * 2
    walk_down
    step_end
}

text MaisyHouse_Text_TheseTwo {
    format("MAISY: In anticipation of you\nstarting your journey as a POKéMON\lTRAINER, grandpa ordered...\pAh! A TOWN MAP and a POKéDEX!\pI'll just set them down over here!")
}

text MaisyHouse_Text_Take {
    format("MAISY: Alrighty!\nGo ahead and take them!")
}

script MaisyHouse_EventScript_TownMap {
    lock
    msgbox(MaisyHouse_Text_TakeMap, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, YES, MaisyHouse_EventScript_TownMap2)
    release
    end
}

script MaisyHouse_EventScript_TownMap2 {
    removeobject(VAR_LAST_TALKED)
    //setflag(FLAG_HIDE_TOWN_MAP)
    giveitem_msg(MaisyHouse_Text_ReceivedMap, ITEM_TOWN_MAP, 1, MUS_OBTAIN_KEY_ITEM)
    addvar(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1)
    call_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, MaisyHouse_EventScript_MaisyToPlayer)
    goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, MaisyHouse_EventScript_GotBoth)
    release
    end
}

script MaisyHouse_EventScript_PokeDex {
    lock
    msgbox(MaisyHouse_Text_TakeDex, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, YES, MaisyHouse_EventScript_PokeDex2)
    release
    end
}

script MaisyHouse_EventScript_PokeDex2 {
    removeobject(VAR_LAST_TALKED)
    //setflag(FLAG_HIDE_POKEDEX)
    message(MaisyHouse_Text_ReceivedDex)
	waitmessage
	playfanfare(MUS_OBTAIN_KEY_ITEM)
	waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_SYS_NATIONAL_DEX)
    addvar(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 1)
    goto_if_eq(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, MaisyHouse_EventScript_MaisyToPlayer)
}

script MaisyHouse_EventScript_MaisyToPlayer {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    call_if_eq(VAR_TEMP_0, 5, MaisyHouse_EventScript_MaisyUpRight)
    call_if_eq(VAR_TEMP_0, 6, MaisyHouse_EventScript_MaisyUpRight2)
    call_if_eq(VAR_TEMP_0, 7, MaisyHouse_EventScript_MaisyUpRight3)
    goto(MaisyHouse_EventScript_GotBoth)
}

script MaisyHouse_EventScript_MaisyUpRight {
    applymovement(DAISY_ID, MaisyHouse_Movement_DaisyUpRight)
    waitmovement(0)
    return
}

script MaisyHouse_EventScript_MaisyUpRight2 {
    applymovement(DAISY_ID, MaisyHouse_Movement_DaisyUpRight2)
    waitmovement(0)
    return
}

script MaisyHouse_EventScript_MaisyUpRight3 {
    applymovement(DAISY_ID, MaisyHouse_Movement_DaisyUpRight3)
    waitmovement(0)
    return
}

movement MaisyHouse_Movement_DaisyUpRight {
    walk_up
    step_end
}

movement MaisyHouse_Movement_DaisyUpRight2 {
    walk_up
    walk_right
    step_end
}

movement MaisyHouse_Movement_DaisyUpRight3 {
    walk_up
    walk_right * 3
    step_end
}

script MaisyHouse_EventScript_GotBoth {
    call_if_le(VAR_TEMP_0, 6, MaisyHouse_EventScript_LeftTwo)
    call_if_ge(VAR_TEMP_0, 7, MaisyHouse_EventScript_RightOne)
    textcolor(NPC_TEXT_COLOR_FEMALE)
    msgbox(MaisyHouse_Text_GiveBalls)
    giveitem(ITEM_POKE_BALL, 5)
    msgbox(MaisyHouse_Text_GoOn)
    release
    end
}

script MaisyHouse_EventScript_LeftTwo {
    applymovement(DAISY_ID, Common_Movement_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    waitmovement(0)
    return
}

script MaisyHouse_EventScript_RightOne {
    applymovement(DAISY_ID, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    return  
}

text MaisyHouse_Text_TakeMap {
    format("The TOWN MAP can be used to view\nthe map of the entire region.\pTake the TOWN MAP?")
}

text MaisyHouse_Text_TakeDex {
    format("The POKéDEX automatically records\ndata on any POKéMON when they are\lcaught.\pTake the POKéDEX?")
}

text MaisyHouse_Text_ReceivedMap {
    format("{PLAYER} received the TOWN MAP!")
}

text MaisyHouse_Text_ReceivedDex {
    format("{PLAYER} received the POKéDEX!")
}

text MaisyHouse_Text_GiveBalls {
    format("MAISY: Oh! I almost forgot these!")
}

text MaisyHouse_Text_GoOn {
    format("MAISY: That should be everything!\nGood luck on your adventure,\l{PLAYER}!\pI know that {RIVAL} left on his\n"
    "adventure as well, but grandpa and\lI wanted to give these to you.\pYou helped grandpa out with so many things, while {RIVAL} did nothing but mock everyone.\pI hope he learns his lesson some\nday, or he might get into some\lserious trouble.\pEither way, I'm rooting for you,\n{PLAYER}!")
}

script MaisyHouse_EventScript_Maisy {
    lock
    goto_if_le(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 2, MaisyHouse_EventScript_MaisyTake)
    goto_if_ge(VAR_MAP_SCENE_VIRIDIAN_CITY_OLD_MAN, 3, MaisyHouse_EventScript_MaisyNormal)
}

script MaisyHouse_EventScript_MaisyTake {
    msgbox(MaisyHouse_Text_Take, MSGBOX_NPC)
}

//Probably can enhance this later
script MaisyHouse_EventScript_MaisyNormal { 
    msgbox(MaisyHouse_Text_MaisyNormal, MSGBOX_NPC)
}

text MaisyHouse_Text_MaisyNormal {
    format("MAISY: I'm cheering for you,\n{PLAYER}!")
}